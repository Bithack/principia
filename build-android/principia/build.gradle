plugins {
    id 'com.android.application'
    id 'de.undercouch.download'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.bithack.principia"
        minSdk 19
        targetSdk 32
        versionCode 36
        versionName "1.6"

        externalNativeBuild {
            ndkBuild {
                arguments '-j' + Runtime.getRuntime().availableProcessors()
            }
        }

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    externalNativeBuild {
        ndkBuild {
            path = file("../jni/Android.mk")
        }
    }

    sourceSets {
        main {
            assets.srcDirs = ['src/assets']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.0'
}

// get precompiled deps
task downloadDeps(type: Download) {
	src 'https://github.com/principia-preservation-project/principia_android_deps/releases/download/latest/deps.zip'
	dest new File(buildDir, 'deps.zip')
	overwrite false
}

task getDeps(dependsOn: downloadDeps, type: Copy) {
	def deps = new File(buildDir.parent, '../deps')
	if (!deps.exists()) {
		deps.mkdir()
		from zipTree(downloadDeps.dest)
		into deps
	}
}

preBuild.dependsOn getDeps
