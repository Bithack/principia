plugins {
    id 'com.android.application'
    id 'de.undercouch.download'
}

android {
    compileSdk 33
    ndkVersion "26.2.11394342"

    defaultConfig {
        applicationId "com.bithack.principia"
        minSdk 21
        targetSdk 33
        versionCode 35
        versionName "2024.02.29"

        externalNativeBuild {
            ndkBuild {
                arguments '-j' + Runtime.getRuntime().availableProcessors()
            }
        }

        ndk {
            abiFilters "arm64-v8a", "armeabi-v7a", "x86_64"
            //abiFilters "arm64-v8a" // debugging on phone
            //abiFilters "x86_64"    // debugging on emulator
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    externalNativeBuild {
        cmake {
            path file("../../CMakeLists.txt")
        }
    }

    sourceSets {
        main {
            assets.srcDirs = ['src/assets']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.0'
}

// get precompiled deps
task downloadDeps(type: Download) {
	src 'https://github.com/principia-game/principia-android-deps/releases/download/latest/deps.zip'
	dest new File(buildDir, 'deps.zip')
	overwrite false
}

task getDeps(dependsOn: downloadDeps, type: Copy) {
	def deps = new File(buildDir.parent, '../deps')
	if (!deps.exists()) {
		deps.mkdir()
		from zipTree(downloadDeps.dest)
		into deps
	}
}

preBuild.dependsOn getDeps
