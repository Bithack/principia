plugins {
    id 'com.android.application'
    id 'de.undercouch.download'
}

android {
    compileSdk 33
    ndkVersion "23.2.8568313"

    defaultConfig {
        applicationId "com.bithack.principia"
        minSdk 19
        targetSdk 33
        versionCode 34
        versionName "1.5.2 Beta"

        externalNativeBuild {
            ndkBuild {
                arguments '-j' + Runtime.getRuntime().availableProcessors()
            }
        }

        ndk {
            abiFilters "arm64-v8a", "armeabi-v7a"
            // "x86", "x86-64"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    externalNativeBuild {
        ndkBuild {
            path = file("../jni/Android.mk")
        }
    }

    sourceSets {
        main {
            assets.srcDirs = ['src/assets']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.0'
}

// get precompiled deps
task downloadDeps(type: Download) {
	src 'https://github.com/principia-game/principia-android-deps/releases/download/latest/deps.zip'
	dest new File(buildDir, 'deps.zip')
	overwrite false
}

task getDeps(dependsOn: downloadDeps, type: Copy) {
	def deps = new File(buildDir.parent, '../deps')
	if (!deps.exists()) {
		deps.mkdir()
		from zipTree(downloadDeps.dest)
		into deps
	}
}

preBuild.dependsOn getDeps
